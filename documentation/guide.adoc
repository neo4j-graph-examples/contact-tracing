= A Neo4j Browser Guide to explore a Contact Tracing database

== Contact Tracing - Background

image::{img}/tracing.jpg[width=400,float=right]

This Sandbox is based on a series of blog posts about Contact Tracing With Neo4j written by https://twitter.com/rvanbruggen^[Rik Van Bruggen]. For more detail please refer to:

* https://blog.bruggen.com/2020/04/covid-19-contact-tracing-blogpost-part.html[Part 1^]: How to create a synthetic dataset and import that into Neo4j
* https://blog.bruggen.com/2020/04/covid-19-contact-tracing-blogpost-part_21.html[Part 2^]: How to start running some interesting queries on the dataset. Understand some of the interesting data points in there and questions that one might ask
* https://blog.bruggen.com/2020/04/covid-19-contact-tracing-blogpost-part_61.html[Part 3^]: How to use Graph Data Science on this dataset, and understand some of the predictive metrics like PageRank, Betweenness Centrality and use community detection to direct policies.
* https://blog.bruggen.com/2020/04/covid-19-contact-tracing-blogpost-part_0.html[Part 4^]: A number of loose ends touched on during exploration of the dataset - but surely not exhaustive.

In this guide we show you the statements in an orderly sequence. First we start with basic Cypher queries to answer questions about finding sick people, where they've traveled and who they may have infected. We then see how to use the Neo4j Graph Data Science library to use graph algorithms to find superspreaders and communities within the graph that can inform policy decisions. Finally we use Neo4j Bloom to visualize the graph, including the results of algorithms.

Let's get started.

== Contact Tracing - The Data Model

First, let's look at the data model of the synthetic dataset.

[source,cypher]
----
CALL db.schema.visualization()
----

By running the `db.schema.visualization` procedure we can see the node labels and relationship types used in the dataset.

**How many sick and healthy people in the dataset?**

[source, cypher]
----
MATCH (p:Person)
RETURN DISTINCT p.healthstatus, count(*);
----

== Querying Data - Potential Infection Spread

**Who has a sick person potentially infected?**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})
WITH p LIMIT 1
MATCH (p)--(v1:Visit)--(pl:Place)--(v2:Visit)--(p2:Person {healthstatus:"Healthy"})
RETURN p.name as Spreader, v1.starttime as SpreaderStarttime, v2.endtime as SpreaderEndtime, pl.name as PlaceVisited, p2.name as Target, v2.starttime as TargetStarttime, v2.endtime as TargetEndttime;
----

**Who has a sick person potentially infected? Visual result**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})
WITH p LIMIT 1
MATCH path = (p)-->(v1:Visit)-->(pl:Place)<--(v2:Visit)<--(p2:Person {healthstatus:"Healthy"})
RETURN path;
----

**We can simplify the query using the `VISITS` relationship**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})
WITH p LIMIT 1
MATCH path = (p)-[:VISITS]->(pl:Place)<-[:VISITS]-(p2:Person {healthstatus:"Healthy"})
RETURN path;
----

== Potential Infection Spread - With Time Overlap

**Including Time Overlap**

The latest of start times must occur before (or at the same time) as the earliest of the end times for the ranges to overlap.

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-->(v1:Visit)-->(pl:Place)
WITH p,v1,pl LIMIT 10
MATCH path = (p)-->(v1)-->(pl)<--(v2:Visit)<--(p2:Person {healthstatus:"Healthy"})
WITH path, apoc.coll.max([v1.starttime.epochMillis, v2.starttime.epochMillis]) as maxStart,
     apoc.coll.min([v1.endtime.epochMillis, v2.endtime.epochMillis]) as minEnd
WHERE maxStart <= minEnd
RETURN path;
----
++++
<br><br>
++++

**Simplify With `VISITS` Relationship**

Who has a sick person infected - with time overlap and simplified with the `VISITS` relationship. The latest of start times must occur before (or at the same time) as the earliest of the end times for the ranges to overlap.

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-[v1:VISITS]->(pl:Place)
WITH p,v1,pl LIMIT 10
MATCH path = (p)-[v1]->(pl)<-[v2:VISITS]-(p2:Person {healthstatus:"Healthy"})
WITH path, apoc.coll.max([v1.starttime.epochMillis, v2.starttime.epochMillis]) as maxStart,
     apoc.coll.min([v1.endtime.epochMillis, v2.endtime.epochMillis]) as minEnd
WHERE maxStart <= minEnd
RETURN path;
----

++++
<br><br>
++++

**With Time Overlap +/- 2 Hours**

Who has a sick person infected - with time overlap +/- 2hrs. The latest of start times must occur before (or at the same time) as the earliest of the end times for the ranges to overlap.

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-->(s1:Visit)-->(pl:Place)
WITH p,s1,pl LIMIT 10
MATCH path = (p)-->(s1)-->(pl)<--(s2:Visit)<--(p2:Person {healthstatus:"Healthy"})
WITH path, apoc.coll.max([s1.starttime.epochMillis, s2.starttime.epochMillis]) as maxStart,
     apoc.coll.min([s1.endtime.epochMillis, s2.endtime.epochMillis]) as minEnd
WHERE maxStart-720000 <= minEnd+720000
RETURN path;
----

== Tracking Visits By Sick Persons

**Find sick person that has visited places since being infected**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-[v:VISITS]->(pl:Place)
WHERE p.confirmedtime < v.starttime
RETURN p, v, pl LIMIT 10;
----

**Find connections between sick people**

[source, cypher]
----
MATCH (p1:Person {healthstatus:"Sick"}),(p2:Person {healthstatus:"Sick"})
WHERE id(p1)<id(p2)
WITH p1, p2
MATCH path = allshortestpaths ((p1)-[*]-(p2))
RETURN path LIMIT 10; 
----

== Measuring Infection Risk - Time Overlap

**Which healthy person has the highest risk?**

We can compute this based on the amount of time overlapped with sick people.

[source, cypher]
----
MATCH (hp:Person {healthstatus:"Healthy"})-[v1:VISITS]->(pl:Place)<-[v2:VISITS]-(sp:Person {healthstatus:"Sick"})
WITH hp, apoc.coll.max([v1.starttime.epochMillis, v2.starttime.epochMillis]) AS maxStart,
     apoc.coll.min([v1.endtime.epochMillis, v2.endtime.epochMillis]) AS minEnd
WHERE maxStart <= minEnd
RETURN hp.name, hp.healthstatus, sum(minEnd-maxStart) AS overlaptime
ORDER BY overlaptime DESC;
----


**Which healthy person has the highest risk? Visual results**

[source, cypher]
----
MATCH (hp:Person {healthstatus:"Healthy"})-[v1:VISITS]->(pl:Place)<-[v2:VISITS]-(sp:Person {healthstatus:"Sick"})
WITH hp, apoc.coll.max([v1.starttime.epochMillis, v2.starttime.epochMillis]) AS maxStart,
     apoc.coll.min([v1.endtime.epochMillis, v2.endtime.epochMillis]) AS minEnd
WHERE maxStart <= minEnd
WITH hp, sum(minEnd-maxStart) AS overlaptime
ORDER BY overlaptime DESC
LIMIT 10
MATCH (hp)-[v]-(pl:Place)
RETURN hp,v,pl;
----

== Measuring Infection Risk - Places 

**Places with most sick visits.**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-[v:VISITS]->(pl:Place)
WITH DISTINCT pl.name as placename, count(v) AS nrofsickvisits, apoc.node.degree.in(pl,'VISITS') AS totalnrofvisits
ORDER BY nrofsickvisits DESC LIMIT 10
RETURN placename, nrofsickvisits, totalnrofvisits, round(toFloat(nrofsickvisits)/toFloat(totalnrofvisits)*10000)/100 AS percentageofsickvisits;
----

**Places with most sick visits. Visual result**

[source, cypher]
----
MATCH (p:Person {healthstatus:"Sick"})-[v:VISITS]->(pl:Place)
WITH DISTINCT pl.name as placename, count(v) AS nrofsickvisits, pl
ORDER BY nrofsickvisits DESC
LIMIT 10
MATCH (pl)<-[v]-(p:Person)
RETURN pl,p,v;
----

== Graph Data Science on the Contact Tracing Graph

The Neo4j Graph Data Science (GDS) library allows us to run graph algorithms like centralities, community detection, similarity metics and more. Let's see how we can use GDS on our Contact Tracing dataset.

**REQUIREMENT: create the MEETS relationship based on overlap time**

This is a relationship between two PERSON nodes that we will need for our graph data science exercises so be sure to run this Cypher query before moving forward

[source, cypher]
----
MATCH (p1:Person)-[v1:VISITS]->(pl:Place)<-[v2:VISITS]-(p2:Person)
WHERE id(p1)<id(p2)
WITH p1, p2, apoc.coll.max([v1.starttime.epochMillis, v2.starttime.epochMillis]) AS maxStart,
    apoc.coll.min([v1.endtime.epochMillis, v2.endtime.epochMillis]) AS minEnd
WHERE maxStart <= minEnd
WITH p1, p2, sum(minEnd-maxStart) AS meetTime
CREATE (p1)-[:MEETS {meettime: duration({seconds: meetTime/1000})}]->(p2);
----

== Graph Data Science: PageRank

**Computing PageRank of Person Nodes**

[source,cypher]
----
CALL gds.pageRank.write({
  nodeProjection: 'Person',
  relationshipProjection: {
  relType: {
    type: 'MEETS',
    orientation: 'NATURAL',
    properties: {}
  }
  },
    relationshipWeightProperty: null,
    dampingFactor: 0.85,
    maxIterations: 20,
    writeProperty: 'pagerank'
  });
----

== Graph Data Science: PageRank

**Look at the top Person nodes by PageRank - tabular results**

[source, cypher]
----
MATCH (p:Person) WHERE EXISTS(p.pagerank)
RETURN p.name as name, p.pagerank AS pagerank
ORDER BY pagerank DESC LIMIT 10;
----

**Look at the top `Person` nodes by PageRank - graph results**

[source, cypher]
----
MATCH (p:Person) WHERE EXISTS(p.pagerank)
WITH p, p.pagerank AS score
ORDER BY score DESC LIMIT 10
MATCH (p)-[r]-(conn)
RETURN p, r, conn;
----


== Graph Data Science: Betweenness Centrality

**Compute Betweenness Centrality**

[source,cypher]
----
CALL gds.alpha.betweenness.write({
  nodeProjection: 'Person',
  relationshipProjection: {
  relType: {
    type: 'MEETS',
    orientation: 'NATURAL',
    properties: {}
  }
  },
    writeProperty: 'betweenness'
  });
----

== Graph Data Science: Betweenness Centrality

**Look at the top `Person` nodes by betweenness centrality - tabular results**

[source, cypher]
----
MATCH (p:Person) WHERE EXISTS(p.betweenness)
RETURN p.name as name, p.pagerank AS pagerank, p.betweenness AS betweenness
ORDER BY betweenness DESC
LIMIT 10;
----

**Look at the top `Person` nodes by betweeness centrality - graph results**

[source, cypher]
----
MATCH (p:Person) WHERE EXISTS(p.betweenness)
WITH p, p.betweenness AS score
ORDER BY score DESC LIMIT 10
MATCH (p)-[r]-(conn)
RETURN p, r, conn;
----


== Graph Data Science: Louvain Community Detection

Since the Louvain algorithm can take advantage of relationship weights, let's add a relationship property to our `MEETS` relationship.

[source, cypher]
----
MATCH p=()-[r:MEETS]->() 
SET r.meettimeinseconds = r.meettime.seconds;
----

**Now we can calculate communities using the Louvain algorithm**

[source,cypher]
----
CALL gds.louvain.write({
  nodeProjection: 'Person',
  relationshipProjection: {
    relType: {
      type: 'MEETS',
      orientation: 'NATURAL',
      properties: {
        meettimeinseconds: {
          property: 'meettimeinseconds',
          defaultValue: 1
        }
      }
    }
  },
  relationshipWeightProperty: 'meettimeinseconds',
  includeIntermediateCommunities: false,
  seedProperty: '',
  writeProperty: 'louvain'
});
----

== Graph Data Science - Louvain Community Detection

**What are the different communities?**

[source, cypher]
----
MATCH (p:Person)
RETURN DISTINCT p.louvain, count(p)
ORDER BY COUNT(p) DESC;
----

== Neo4j Bloom

So far we've seen how to visualize query results in Neo4j Browser, however we are somewhat limited in the types of visualization available in Neo4j Browser. Fortunately we can use Neo4j Bloom to explore the graph visually, especially taking into account the results of graph algorithms in the visualization.

Let's switch over to Neo4j Bloom to explore some powerful graph visualizations.

== Resources

Resources to learn more:

* http://blog.bruggen.com[Rik's blog^]
* https://twitter.com/rvanbruggen[Rik on Twitter^]
* http://graphistania.com[The Graphistania podcast^]